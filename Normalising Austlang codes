USE CASE: Normalise Austlang codes to "lang.xxx.[#,x]" ensuring 'aus' and other languages are copied

EXAMPLE RECORD: A record contains the following dc:language elements with different language attributes.

	<dc:language>en</dc:language>
	<dc:language>aus</dc:language>
	<dc:language type="austlang">N116.S</dc:language>
			
				OR

	<dc:language>en</dc:language>
	<dc:subject>Miliwuru language N116.S</dc:subject>

USING THESE FOUR RULES COMBINDED:

rule "1: Copy dc:language (not 'aus' as added in rule 3 or 4) - see below for example result"
 when
	"//*[local-name()='language'][1]/text()" not equals "aus" AND exist "//*[local-name()='language'][not(@type='austlang')][1]/text()"
  then
	set TEMP"1" to xpath "//*[local-name()='language'][not(@type='austlang')][1]/text()"
	add prefix (TEMP"1","lang.")
	lower case (TEMP"1")
	set TEMP"1" in "dc"."language"
 end

rule "2: Copy dc:language (if second language - not 'aus')"
 when
	"//*[local-name()='language'][2]/text()" not equals "aus" AND exist "//*[local-name()='language'][not(@type='austlang')][2]/text()"
 then
	set TEMP"1" to xpath "//*[local-name()='language'][not(@type='austlang')][2]/text()"
	add prefix (TEMP"1","lang.")
	lower case (TEMP"1")
	set TEMP"1" in "dc"."language"
 end

rule "3: normalise Austlang code"
 when
	exist "//*[local-name()='language'][@type='austlang']/text()" 
 then
	set TEMP"1" to xpath "//*[local-name()='language'][@type='austlang']/text()"
	add prefix (TEMP"1","lang.")
	lower case (TEMP"1")
	set TEMP"1" in "dc"."language"
    set "lang.aus" in "dc"."language"
 end

rule "4: Contains Austlang in subject"
 when 
	exist "//*[local-name()='subject'][contains(text(),'language')]" AND not exist "//*[local-name()='language'][@type='austlang']/text()"
 then
	set TEMP"1" to xpath "//*[local-name()='subject'][contains(text(),'language')]/text()"
    remove substring using regex (TEMP"1","(.*language.)")
	add prefix (TEMP"1","lang.")
	lower case (TEMP"1")
	set TEMP"1" in "dc"."language"
    set "lang.aus" in "dc"."language"
 end

WILL RETURN (FOR BOTH EXAMPLES):

	<dc:language>lang.n116.s</dc:language>
	<dc:language>en</dc:language>
	<dc:language>aus</dc:language>
	
*This rule was contributed by https://github.com/LincolnUniLTL through the following issue: https://github.com/primousers/primo-ve-norm/issues/5
